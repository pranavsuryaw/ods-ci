*** Settings ***
Resource            ../../Resources/ODS.robot
Resource            ../../Resources/Common.robot
Resource            ../../Resources/Page/ODH/JupyterHub/JupyterHubSpawner.robot
Resource            ../../Resources/Page/ODH/JupyterHub/JupyterLabLauncher.robot
Library             DebugLibrary

Suite Setup         Server Setup
Suite Teardown      End Web Test


*** Variables ***
${PATH_TO_FILE}         ods-ci-notebooks-main/notebooks/200__monitor_and_manage/203__alerts/notebook-pvc-usage/fill-notebook-pvc-to-complete-100.ipynb
${LINK_OF_GITHUB}       https://github.com/redhat-rhods-qe/ods-ci-notebooks-main.git
${FILE_NAME}            fill-notebook-pvc-to-complete-100.ipynb


*** Test Cases ***
Get Notifications If Storage Capacity Limits Get Exceeded
    [Documentation]    It clones the ${LINK_OF_GITHUB} repo and runs ${FILE_NAME}, then it checks for the OS Error
    ...    which is generated by running that file and also checks for the popup which shows File Save Error
    [Tags]    Sanity    
    ...       ODS-539

    ${error} =    Run Git Repo And Return Last Cell Error Text    ${LINK_OF_GITHUB}    ${PATH_TO_FILE}
    Should Be Equal    OSError: [Errno 28] No space left on device    ${error}
    Wait Until Page Contains    File Save Error for ${FILE_NAME}    timeout=150s
    Maybe Close Popup
    Clean Up User Notebook    ${OCP_ADMIN_USER.USERNAME}    ${TEST_USER.USERNAME}


*** Keywords ***
Server Setup
    [Documentation]    Suite Setup
    Begin Web Test
    Launch JupyterHub Spawner From Dashboard
    Spawn Notebook With Arguments    image=s2i-minimal-notebook    size=Default
